apply plugin: 'java'


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(":nanolrs-core")
    compile 'org.nanohttpd:nanohttpd:2.3.1'
    compile 'org.nanohttpd:nanohttpd-nanolets:2.3.1'
    testCompile project(":nanolrs-core-tests")
    testCompile 'junit:junit:4.12'
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

//Expose test classes via a configuration and jar
task testJar(type: Jar, dependsOn: testClasses) {
    baseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
    exclude("com/ustadmobile/nanolrs/core/util/NanoLrsPlatformTestUtil.class")
}

configurations {
    tests
}

artifacts {
    tests testJar
}


apply plugin: 'maven-publish'

publishing {
    publications {
        nanolrscore(MavenPublication) {
            groupId 'com.ustadmobile.nanolrs'
            artifactId 'nanolrs-http'
            version rootProject.ext.nanolrsversion

            from components.java

            artifact sourceJar {
                classifier = 'sources'
            }
        }
    }

    repositories {
        //Set this in projectdir/extrasettings.gradle
        maven {
            url rootProject.ext.repoDir
        }
    }
}
