
apply plugin: 'com.android.library'
apply plugin: "jacoco"
apply plugin: 'maven-publish'


android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "TESTUSER", '"' + rootProject.ext.buildConfigProperties["test.remotelrs.user"] + '"'
        buildConfigField "String", "TESTPASSWORD", '"' + rootProject.ext.buildConfigProperties["test.remotelrs.password"] + '"'
        buildConfigField "String", "TESTLRSENDPOINT", '"' + rootProject.ext.buildConfigProperties["test.remotelrs.endpoint"] + '"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled = true
        }
    }

    lintOptions {
        abortOnError false
    }
}

jacoco {
    toolVersion = "0.7.9"
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    implementation group: 'com.android.support', name: "appcompat-v7", version: "[26+, 27+]"
    implementation group: 'com.android.support', name: "appcompat-v7", version: "[26+, 27+]"
    implementation project(":nanolrs-ormlite")
    implementation project(":nanolrs-http")
    androidTestImplementation project(":nanolrs-core-tests")
    implementation "com.j256.ormlite:ormlite-android:5.0"

    implementation 'com.evernote:android-job:1.1.11'
}


task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

task androidReportsZip(type: Zip) {
    from "$buildDir/"
    archiveName "android-test-outputs.zip"
    include "reports/coverage/**"
    include "reports/androidTests/**"
}

publish.finalizedBy androidReportsZip

publishing {
    publications {

        //Taken from: http://stackoverflow.com/questions/26874498/maven-publish-android-library-with-aar-and-source-jar

        nanolrsandroid(MavenPublication) {
            groupId 'com.ustadmobile.nanolrs'
            artifactId 'nanolrs-android'
            version rootProject.ext.nanolrsversion
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/nanolrs-android-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    if(it.group != null && (it.name != null || "unspecified".equals(it.name)) && it.version != null)
                    {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        if(it.group.equals("workspace") || it.group.equals("NanoLRS")) {
                            dependencyNode.appendNode('groupId', "com.ustadmobile.nanolrs")
                        }else {
                            dependencyNode.appendNode('groupId', it.group)
                        }
                        
                        dependencyNode.appendNode('artifactId', it.name)
                        if(it.version.equals("unspecified")) {
                            dependencyNode.appendNode('version', rootProject.ext.nanolrsversion)
                        }else {
                            dependencyNode.appendNode('version', it.version)
                        }

                    }
                }
            }

        }
    }
    repositories {
        //Set this in projectdir/extrasettings.gradle
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}
