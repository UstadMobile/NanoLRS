apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: "jacoco"

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.7'
    }
}

ext.buildConfigProperties = new Properties()
ext.buildConfigProperties.load(new FileInputStream(project.file("buildconfig.default.properties")))
if(project.file("buildconfig.local.properties").exists()) {
    ext.buildConfigProperties.load(new FileInputStream(project.file("buildconfig.local.properties")))
}

task generateCoreTestConstantsBuildConfig (type: de.fuerstenau.gradle.buildconfig.GenerateBuildConfigTask) {
    inputs.file("buildconfig.default.properties")
    inputs.file("buildconfig.local.properties")
    outputDir = new File("${projectDir}/src/test/java")
    clsName = "TestConstantsJDBC"
    packageName = "com.ustadmobile.nanolrs.buildconfig"

    buildConfigField "String", "TEST_JDBC_URL", project.ext.buildConfigProperties["test.jdbc.url"]
}
compileTestJava.dependsOn(generateCoreTestConstantsBuildConfig)


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(":nanolrs-core")
    compile project(":nanolrs-ormlite")
    ext.testDbDependency = project.ext.buildConfigProperties['test.jdbc.dbDriverDependency']
    testCompile "$testDbDependency"
    testCompile 'junit:junit:4.12'
    //compile project(":nanolrs-core-tests")
    testCompile project(":nanolrs-core-tests")
    compile "com.j256.ormlite:ormlite-core:5.0"
    compile "com.j256.ormlite:ormlite-jdbc:5.0"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava

}

publishing {
    publications {
        nanolrscore(MavenPublication) {
            groupId 'com.ustadmobile.nanolrs'
            artifactId 'nanolrs-jdbc'
            version rootProject.ext.nanolrsversion

            from components.java

            artifact sourceJar {
                classifier = 'sources'
            }
        }
    }

    repositories {
        //Set this in projectdir/extrasettings.gradle
        maven {
            url rootProject.ext.repoDir
        }
    }
}
