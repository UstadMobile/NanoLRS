
buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.7'
    }
}


apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = 1.7

task generateCoreTestConstantsBuildConfig (type: de.fuerstenau.gradle.buildconfig.GenerateBuildConfigTask) {
    inputs.file("../buildconfig.default.properties")
    inputs.file("../buildconfig.local.properties")
    outputDir = new File("${projectDir}/src/test/java")
    clsName = "TestConstantsCore"
    packageName = "com.ustadmobile.nanolrs.buildconfig"
    buildConfigField "String", "TEST_JDBC_URL", rootProject.ext.buildConfigProperties["test.jdbc.url"]
    buildConfigField "String", "TESTUSER", rootProject.ext.buildConfigProperties["test.remotelrs.user"]
    buildConfigField "String", "TESTPASSWORD", rootProject.ext.buildConfigProperties["test.remotelrs.password"]
    buildConfigField "String", "TESTLRSENDPOINT", rootProject.ext.buildConfigProperties["test.remotelrs.endpoint"]
}
compileTestJava.dependsOn(generateCoreTestConstantsBuildConfig)



dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.json:json:20160212";
    testCompile 'junit:junit:4.12'
    testCompile project(":nanolrs-ormlite")
    testCompile project(":nanolrs-jdbc")
    testCompile "com.j256.ormlite:ormlite-core:5.0"
    testCompile "com.j256.ormlite:ormlite-jdbc:5.0"
    ext.testDbDependency = rootProject.ext.buildConfigProperties["test.jdbc.dbDriverDependency"]
    testCompile "$testDbDependency"
}

jar  {
    exclude("com/ustadmobile/nanolrs/core/persistence/PersistenceManagerFactoryImpl.class")
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava

}

jacoco {
    reportsDir = file("$buildDir/reports/jacoco")
}

test {
    include "com/ustadmobile/nanolrs/core/*/*Test**"
}

//Expose test classes via a configuration and jar
task testJar(type: Jar, dependsOn: testClasses) {
    baseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
    exclude("com/ustadmobile/nanolrs/core/util/NanoLrsPlatformTestUtil.class")

}

configurations {
    tests
}

artifacts {
    tests testJar
}

apply plugin: 'maven-publish'

publishing {
    publications {
        nanolrscore(MavenPublication) {
            groupId 'com.ustadmobile.nanolrs'
            artifactId 'nanolrs-core'
            version rootProject.ext.nanolrsversion

            from components.java

            artifact sourceJar {
                classifier = 'sources'
            }
        }
    }

    repositories {
        //Set this in projectdir/extrasettings.gradle
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}

